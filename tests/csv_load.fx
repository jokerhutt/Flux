// Author: Karac V. Thweatt

// csv_load.fx - Load sample.csv and benchmark parse speed.

#import <standard.fx>;
#import <ffifio.fx>;
#import <csv.fx>;
#import <timing.fx>;

using standard::io::console,
      standard::io::file,
      standard::strings,
      standard::time;

using csv;

def print_ms(i64 ns) -> void
{
    i64      ms, us;
    byte[64] buf;
    ms = ns_to_ms(ns);
    us = ns_to_us(ns) % 1000;
    i64str(ms, @buf[0]);
    print(@buf[0]);
    print(".\0");
    if (us < 100) { print("0\0"); };
    if (us < 10)  { print("0\0"); };
    i64str(us, @buf[0]);
    print(@buf[0]);
    print(" ms\0");
};

def print_mbs(i64 bytes, i64 ns) -> void
{
    i64      us, kb_per_s, mbs, mbs_frac;
    byte[64] buf;
    if (ns <= 0) { print("N/A\0"); return; };
    us = ns / 1000;
    if (us <= 0) { us = 1; };
    kb_per_s = (bytes * 1000) / us;
    mbs      = kb_per_s / 1024;
    mbs_frac = (kb_per_s % 1024) * 10 / 1024;
    i64str(mbs, @buf[0]);
    print(@buf[0]);
    print(".\0");
    i64str(mbs_frac, @buf[0]);
    print(@buf[0]);
    print(" MB/s\0");
};

def main() -> int
{
    void*    fh;
    int      file_size, bytes_read;
    byte*    buf;
    byte[64] num_buf;
    CsvTable table;
    i64      t_start, t_after_load, t_after_parse, load_ns, parse_ns, total_ns;

    t_start = time_now();

    print("Opening sample.csv...\n\0");

    fh = fopen("sample.csv\0", "rb\0");
    if ((u64)fh == 0)
    {
        print("ERROR: Could not open sample.csv\n\0");
        return 1;
    };

    fseek(fh, 0, SEEK_END);
    file_size = ftell(fh);
    fseek(fh, 0, SEEK_SET);

    print("File size: \0");
    i32str(file_size, @num_buf[0]);
    print(@num_buf[0]);
    print(" bytes\n\0");

    buf = (byte*)fmalloc((u64)file_size + 1);
    if ((u64)buf == 0)
    {
        print("ERROR: Out of memory\n\0");
        fclose(fh);
        return 1;
    };

    bytes_read = fread(buf, 1, file_size, fh);
    fclose(fh);
    buf[bytes_read] = (byte)0;

    t_after_load = time_now();

    if (!csv_parse_buf(buf, bytes_read, ',', @table))
    {
        print("ERROR: CSV parse failed\n\0");
        ffree((u64)buf);
        return 1;
    };

    t_after_parse = time_now();

    ffree((u64)buf);

    print("Rows:    \0");
    i32str(table.count, @num_buf[0]);
    print(@num_buf[0]);
    print("\n\0");

    if (table.count > 0)
    {
        print("Columns: \0");
        i32str(table.rows[0].count, @num_buf[0]);
        print(@num_buf[0]);
        print("\n\0");
    };

    csv_free(@table);

    load_ns  = t_after_load  - t_start;
    parse_ns = t_after_parse - t_after_load;
    total_ns = t_after_parse - t_start;

    print("\n\0");
    print("Load:  \0"); print_ms(load_ns);  print(" | \0"); print_mbs((i64)bytes_read, load_ns);  print("\n\0");
    print("Parse: \0"); print_ms(parse_ns); print(" | \0"); print_mbs((i64)bytes_read, parse_ns); print("\n\0");
    print("Total: \0"); print_ms(total_ns); print(" | \0"); print_mbs((i64)bytes_read, total_ns); print("\n\0");

    return 0;
};

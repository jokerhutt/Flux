// Author: Karac V. Thweatt
// toml_load.fx - Load sample.toml, parse it, and time each phase.

#import <standard.fx>, <toml.fx>, <timing.fx>;

using standard::io::console,
      standard::io::file,
      standard::strings,
      standard::time,
      toml;

def print_ms(i64 ns) -> void
{
    i64      ms, us;
    byte[64] buf;
    ms = ns_to_ms(ns);
    us = ns_to_us(ns) % 1000;
    i64str(ms, @buf[0]);
    print(@buf[0]);
    print(".");
    if (us < 100) { print("0"); };
    if (us < 10)  { print("0"); };
    i64str(us, @buf[0]);
    print(@buf[0]);
    print(" ms");
};

def print_mbs(i64 bytes, i64 ns) -> void
{
    i64      us, kb_per_s, mbs, mbs_frac;
    byte[64] buf;
    if (ns <= 0) { print("N/A"); return; };
    us = ns / 1000;
    if (us <= 0) { us = 1; };
    kb_per_s = (bytes * 1000) / us;
    mbs      = kb_per_s / 1024;
    mbs_frac = (kb_per_s % 1024) * 10 / 1024;
    i64str(mbs, @buf[0]);
    print(@buf[0]);
    print(".");
    i64str(mbs_frac, @buf[0]);
    print(@buf[0]);
    print(" MB/s");
};

def print_indent(int depth) -> void
{
    byte[3] sp = [' ', ' ', 0];
    int i;
    while (i < depth) { print(@sp[0]); i++; };
};

def print_val(TomlVal* v, int depth) -> void;

def print_table(TomlTable* t, int depth) -> void
{
    byte[2]  nl = ['\n',0];
    int i;
    while (i < t.count)
    {
        print_indent(depth);
        print(t.keys[i]);
        print(" = ");
        print_val((TomlVal*)t.vals[i], depth);
        print(@nl[0]);
        i++;
    };
};

def print_val(TomlVal* v, int depth) -> void
{
    byte[64] buf;
    byte[2]  nl = ['\n',0];
    if ((u64)v == 0) { print("(null)"); return; };
    if (v.type == TOML_STRING)
    {
        print("\"\0");
        print(v.s);
        print("\"");
    }
    elif (v.type == TOML_DATETIME) { print(v.s); }
    elif (v.type == TOML_BOOL)
    {
        if ((bool)v.i) { print("true"); } else { print("false"); };
    }
    elif (v.type == TOML_INT)
    {
        i64str(v.i, @buf[0]);
        print(@buf[0]);
    }
    elif (v.type == TOML_FLOAT)
    {
        // Print float with limited precision.
        i64 whole, frac;
        bool neg;
        double d;
        d = v.f;
        neg = d < 0.0;
        if (neg) { d = -d; print("-"); };
        whole = (i64)d;
        frac  = (i64)((d - (double)whole) * 1000000.0);
        i64str(whole, @buf[0]);
        print(@buf[0]);
        print(".");
        if (frac < 100000) { print("0"); };
        if (frac < 10000)  { print("0"); };
        if (frac < 1000)   { print("0"); };
        if (frac < 100)    { print("0"); };
        if (frac < 10)     { print("0"); };
        i64str(frac, @buf[0]);
        print(@buf[0]);
    }
    elif (v.type == TOML_ARRAY)
    {
        byte[3] sp;
        sp[0] = ' '; sp[1] = ' '; sp[2] = 0;
        print("[array, ");
        i32str(v.arr.count, @buf[0]);
        print(@buf[0]);
        print(" items]");
    }
    elif (v.type == TOML_TABLE)
    {
        print("{table, ");
        i32str(v.tbl.count, @buf[0]);
        print(@buf[0]);
        print(" keys}");
    }
    else { print("(unknown)"); };
};

def main() -> int
{
    void*      fh;
    int        file_size, bytes_read;
    byte*      buf;
    byte[64]   num_buf;
    TomlTable  table;
    byte*      err;
    i64        t_start, t_after_load, t_after_parse, load_ns, parse_ns, total_ns;
    byte[2]    nl;
    nl[0] = '\n'; nl[1] = 0;

    t_start = time_now();

    print("Opening sample.toml...\n");

    fh = fopen("sample.toml", "rb");
    if ((u64)fh == 0)
    {
        print("ERROR: Could not open sample.toml\n");
        return 1;
    };

    fseek(fh, 0, SEEK_END);
    file_size = ftell(fh);
    fseek(fh, 0, SEEK_SET);

    print("File size: ");
    i32str(file_size, @num_buf[0]);
    print(@num_buf[0]);
    print(" bytes\n");

    buf = (byte*)fmalloc((u64)file_size + 1);
    if ((u64)buf == 0)
    {
        print("ERROR: Out of memory\n");
        fclose(fh);
        return 1;
    };

    bytes_read = fread(buf, 1, file_size, fh);
    fclose(fh);
    buf[bytes_read] = (byte)0;

    t_after_load = time_now();

    if (!toml_parse(buf, bytes_read, @table))
    {
        err = toml_error(@table);
        print("ERROR: TOML parse failed: ");
        if ((u64)err != 0) { print(err); } else { print("(unknown)"); };
        print(@nl[0]);
        toml_free(@table);
        ffree((u64)buf);
        return 1;
    };

    t_after_parse = time_now();
    ffree((u64)buf);

    print("Root keys: ");
    i32str(table.count, @num_buf[0]);
    print(@num_buf[0]);
    print(@nl[0]);
    print(@nl[0]);

    // Print top-level keys and their values.
    print_table(@table, 0);

    toml_free(@table);

    load_ns  = t_after_load  - t_start;
    parse_ns = t_after_parse - t_after_load;
    total_ns = t_after_parse - t_start;

    print(@nl[0]);
    print("Load:  "); print_ms(load_ns);  print(" | "); print_mbs((i64)bytes_read, load_ns);  print(@nl[0]);
    print("Parse: "); print_ms(parse_ns); print(" | "); print_mbs((i64)bytes_read, parse_ns); print(@nl[0]);
    print("Total: "); print_ms(total_ns); print(" | "); print_mbs((i64)bytes_read, total_ns); print(@nl[0]);

    return 0;
};

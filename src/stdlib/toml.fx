// Author: Karac V. Thweatt
//
// toml.fx - TOML v1.0 parse library for Flux.
//
// TomlVal    - tagged value node (string/int/float/bool/datetime/array/table)
// TomlTable  - ordered key/value store
// TomlArray  - growable array of TomlVal pointers
//
// Entry point:
//   toml_parse(byte* src, int len, TomlTable* out) -> bool
//
// Table access:
//   toml_get(TomlTable* t, byte* key) -> TomlVal*
//   toml_get_str(TomlTable* t, byte* key)   -> byte*
//   toml_get_int(TomlTable* t, byte* key)   -> i64
//   toml_get_float(TomlTable* t, byte* key) -> double
//   toml_get_bool(TomlTable* t, byte* key)  -> bool
//   toml_get_table(TomlTable* t, byte* key) -> TomlTable*
//   toml_get_array(TomlTable* t, byte* key) -> TomlArray*
//
// Cleanup:
//   toml_free(TomlTable* t) -> void
//
// Supported:
//   Bare/quoted/dotted keys, basic strings with escapes,
//   literal strings, multi-line basic and literal strings,
//   integers (dec/hex/oct/bin, _ separators), floats (e/inf/nan),
//   booleans, datetime (stored as string), arrays, inline tables,
//   standard tables, array of tables, comments.
//
// Dependencies: standard.fx, allocators.fx

#ifndef FLUX_STANDARD
#import <standard.fx>;
#endif;

#ifndef FLUX_STANDARD_ALLOCATORS
#import <allocators.fx>;
#endif;

#ifndef FLUX_TOML
#def FLUX_TOML 1;

namespace toml
{
    global const int TOML_STRING   = 0;
    global const int TOML_INT      = 1;
    global const int TOML_FLOAT    = 2;
    global const int TOML_BOOL     = 3;
    global const int TOML_DATETIME = 4;
    global const int TOML_ARRAY    = 5;
    global const int TOML_TABLE    = 6;

    // ====================================================================
    // Forward declarations
    // ====================================================================

    struct TomlVal;
    struct TomlTable;
    struct TomlArray;

    def toml_free(TomlTable* t) -> void;

    // ====================================================================
    // Structures
    // ====================================================================

    struct TomlArray
    {
        void**  items;
        int     count;
        int     capacity;
    };

    struct TomlTable
    {
        byte**  keys;
        void**  vals;
        int     count;
        int     capacity;
        byte*   error;
    };

    struct TomlVal
    {
        i64        i;
        double     f;
        byte*      s;
        TomlArray* arr;
        TomlTable* tbl;
        int        type;
    };

    struct _Parser
    {
        byte*      src;
        int        len;
        int        pos;
        int        line;
        TomlTable* root;
        TomlTable* cur;
    };

    // ====================================================================
    // Memory helpers
    // ====================================================================

    def _str_copy(byte* s, int n) -> byte*
    {
        byte* p;
        int   i;
        p = (byte*)fmalloc((size_t)(n + 1));
        if ((u64)p == 0) { return (byte*)0; };
        i = 0;
        while (i < n) { p[i] = s[i]; i++; };
        p[n] = 0;
        return p;
    };

    def _strlen(byte* s) -> int
    {
        int i;
        while (s[i] != 0) { i++; };
        return i;
    };

    def _strcmp(byte* a, byte* b) -> bool
    {
        int i;
        while (a[i] != 0 & b[i] != 0)
        {
            if (a[i] != b[i]) { return false; };
            i++;
        };
        return a[i] == 0 & b[i] == 0;
    };

    // ====================================================================
    // TomlArray helpers
    // ====================================================================

    def _arr_new() -> TomlArray*
    {
        TomlArray* a;
        a = (TomlArray*)fmalloc(sizeof(TomlArray) / 8);
        if ((u64)a == 0) { return (TomlArray*)0; };
        a.items    = (void**)fmalloc((size_t)64);
        a.count    = 0;
        a.capacity = 8;
        return a;
    };

    def _arr_push(TomlArray* a, void* item) -> bool
    {
        void** nb;
        int    new_cap, i;
        if (a.count >= a.capacity)
        {
            new_cap = a.capacity * 2;
            nb      = (void**)fmalloc((size_t)(new_cap * 8));
            if ((u64)nb == 0) { return false; };
            i = 0;
            while (i < a.count) { nb[i] = a.items[i]; i++; };
            ffree((u64)a.items);
            a.items    = nb;
            a.capacity = new_cap;
        };
        a.items[a.count] = item;
        a.count++;
        return true;
    };

    // ====================================================================
    // TomlTable helpers
    // ====================================================================

    def _tbl_new() -> TomlTable*
    {
        TomlTable* t;
        t = (TomlTable*)fmalloc(sizeof(TomlTable) / 8);
        if ((u64)t == 0) { return (TomlTable*)0; };
        t.keys     = (byte**)fmalloc((size_t)64);
        t.vals     = (void**)fmalloc((size_t)64);
        t.count    = 0;
        t.capacity = 8;
        t.error    = (byte*)0;
        return t;
    };

    def _tbl_grow(TomlTable* t) -> bool
    {
        byte** nk;
        void** nv;
        int    new_cap, i;
        new_cap = t.capacity * 2;
        nk = (byte**)fmalloc((size_t)(new_cap * 8));
        nv = (void**)fmalloc((size_t)(new_cap * 8));
        if ((u64)nk == 0 | (u64)nv == 0) { return false; };
        i = 0;
        while (i < t.count) { nk[i] = t.keys[i]; nv[i] = t.vals[i]; i++; };
        ffree((u64)t.keys);
        ffree((u64)t.vals);
        t.keys     = nk;
        t.vals     = nv;
        t.capacity = new_cap;
        return true;
    };

    def _tbl_set(TomlTable* t, byte* key, void* val) -> bool
    {
        int i;
        i = 0;
        while (i < t.count)
        {
            if (_strcmp(t.keys[i], key))
            {
                t.vals[i] = val;
                return true;
            };
            i++;
        };
        if (t.count >= t.capacity)
        {
            if (!_tbl_grow(t)) { return false; };
        };
        t.keys[t.count] = key;
        t.vals[t.count] = val;
        t.count++;
        return true;
    };

    def _tbl_get(TomlTable* t, byte* key) -> void*
    {
        int i;
        i = 0;
        while (i < t.count)
        {
            if (_strcmp(t.keys[i], key)) { return t.vals[i]; };
            i++;
        };
        return (void*)0;
    };

    // ====================================================================
    // TomlVal constructors
    // ====================================================================

    def _val_str(byte* s) -> TomlVal*
    {
        TomlVal* v;
        v = (TomlVal*)fmalloc(sizeof(TomlVal) / 8);
        if ((u64)v == 0) { return (TomlVal*)0; };
        v.type = TOML_STRING; v.s = s;
        return v;
    };

    def _val_int(i64 n) -> TomlVal*
    {
        TomlVal* v;
        v = (TomlVal*)fmalloc(sizeof(TomlVal) / 8);
        if ((u64)v == 0) { return (TomlVal*)0; };
        v.type = TOML_INT; v.i = n;
        return v;
    };

    def _val_float(double f) -> TomlVal*
    {
        TomlVal* v;
        v = (TomlVal*)fmalloc(sizeof(TomlVal) / 8);
        if ((u64)v == 0) { return (TomlVal*)0; };
        v.type = TOML_FLOAT; v.f = f;
        return v;
    };

    def _val_bool(bool b) -> TomlVal*
    {
        TomlVal* v;
        v = (TomlVal*)fmalloc(sizeof(TomlVal) / 8);
        if ((u64)v == 0) { return (TomlVal*)0; };
        v.type = TOML_BOOL; v.i = (i64)b;
        return v;
    };

    def _val_datetime(byte* s) -> TomlVal*
    {
        TomlVal* v;
        v = (TomlVal*)fmalloc(sizeof(TomlVal) / 8);
        if ((u64)v == 0) { return (TomlVal*)0; };
        v.type = TOML_DATETIME; v.s = s;
        return v;
    };

    def _val_array(TomlArray* a) -> TomlVal*
    {
        TomlVal* v;
        v = (TomlVal*)fmalloc(sizeof(TomlVal) / 8);
        if ((u64)v == 0) { return (TomlVal*)0; };
        v.type = TOML_ARRAY; v.arr = a;
        return v;
    };

    def _val_table(TomlTable* t) -> TomlVal*
    {
        TomlVal* v;
        v = (TomlVal*)fmalloc(sizeof(TomlVal) / 8);
        if ((u64)v == 0) { return (TomlVal*)0; };
        v.type = TOML_TABLE; v.tbl = t;
        return v;
    };

    // ====================================================================
    // Cleanup
    // ====================================================================

    def _val_free(TomlVal* v) -> void;

    def _arr_free(TomlArray* a) -> void
    {
        int i;
        i = 0;
        while (i < a.count) { _val_free((TomlVal*)a.items[i]); i++; };
        ffree((u64)a.items);
        ffree((u64)a);
    };

    def _tbl_free_contents(TomlTable* t) -> void
    {
        int i;
        i = 0;
        while (i < t.count)
        {
            ffree((u64)t.keys[i]);
            _val_free((TomlVal*)t.vals[i]);
            i++;
        };
        ffree((u64)t.keys);
        ffree((u64)t.vals);
        if ((u64)t.error != 0) { ffree((u64)t.error); };
    };

    def _val_free(TomlVal* v) -> void
    {
        if ((u64)v == 0) { return; };
        if (v.type == TOML_STRING | v.type == TOML_DATETIME)
        {
            if ((u64)v.s != 0) { ffree((u64)v.s); };
        }
        elif (v.type == TOML_ARRAY)
        {
            if ((u64)v.arr != 0) { _arr_free(v.arr); };
        }
        elif (v.type == TOML_TABLE)
        {
            if ((u64)v.tbl != 0)
            {
                _tbl_free_contents(v.tbl);
                ffree((u64)v.tbl);
            };
        };
        ffree((u64)v);
    };

    def toml_free(TomlTable* t) -> void
    {
        _tbl_free_contents(t);
        t.keys     = (byte**)0;
        t.vals     = (void**)0;
        t.count    = 0;
        t.capacity = 0;
        t.error    = (byte*)0;
    };

    // ====================================================================
    // Parser helpers
    // ====================================================================

    def _is_ws(byte c)      -> bool { return c == ' ' | c == '\t'; };
    def _is_newline(byte c) -> bool { return c == '\n' | c == '\r'; };
    def _is_digit(byte c)   -> bool { return c >= '0' & c <= '9'; };
    def _is_hex(byte c)     -> bool
    {
        return (c >= '0' & c <= '9') | (c >= 'a' & c <= 'f') | (c >= 'A' & c <= 'F');
    };
    def _is_oct(byte c)     -> bool { return c >= '0' & c <= '7'; };
    def _is_bin(byte c)     -> bool { return c == '0' | c == '1'; };
    def _is_bare_key(byte c) -> bool
    {
        return (c >= 'a' & c <= 'z') | (c >= 'A' & c <= 'Z') |
               (c >= '0' & c <= '9') | c == '-' | c == '_';
    };
    def _hex_val(byte c) -> int
    {
        if (c >= '0' & c <= '9') { return (int)(c - '0'); };
        if (c >= 'a' & c <= 'f') { return (int)(c - 'a') + 10; };
        return (int)(c - 'A') + 10;
    };

    def _peek(_Parser* p)  -> byte { if (p.pos >= p.len) { return 0; }; return p.src[p.pos]; };
    def _peek2(_Parser* p) -> byte { if (p.pos + 1 >= p.len) { return 0; }; return p.src[p.pos + 1]; };

    def _adv(_Parser* p) -> void
    {
        if (p.pos < p.len)
        {
            if (p.src[p.pos] == '\n') { p.line++; };
            p.pos++;
        };
    };

    def _skip_ws(_Parser* p) -> void
    {
        while (p.pos < p.len & _is_ws(p.src[p.pos])) { p.pos++; };
    };

    def _skip_ws_newline(_Parser* p) -> void
    {
        while (p.pos < p.len)
        {
            byte c;
            c = p.src[p.pos];
            if (_is_ws(c) | _is_newline(c)) { if (c == '\n') { p.line++; }; p.pos++; }
            elif (c == '#')
            {
                while (p.pos < p.len & p.src[p.pos] != '\n') { p.pos++; };
            }
            else { break; };
        };
    };

    def _skip_line(_Parser* p) -> void
    {
        while (p.pos < p.len & p.src[p.pos] != '\n') { p.pos++; };
        if (p.pos < p.len) { p.line++; p.pos++; };
    };

    def _match(_Parser* p, byte c) -> bool
    {
        if (p.pos < p.len & p.src[p.pos] == c) { p.pos++; return true; };
        return false;
    };

    def _expect(_Parser* p, byte c) -> bool
    {
        return p.pos < p.len & p.src[p.pos] == c;
    };

    def _set_error(_Parser* p, byte* msg) -> void
    {
        if ((u64)p.root.error == 0)
        {
            p.root.error = _str_copy(msg, _strlen(msg));
        };
    };

    // ====================================================================
    // Unicode escape helper
    // ====================================================================

    def _parse_unicode_escape(_Parser* p, byte* buf, int* pos, bool long_form) -> bool
    {
        int   digits, i;
        u64   cp;
        byte  c;
        digits = 4;
        if (long_form) { digits = 8; };
        cp = 0; i = 0;
        while (i < digits)
        {
            if (p.pos >= p.len) { return false; };
            c = p.src[p.pos];
            if (!_is_hex(c)) { return false; };
            cp = cp * 16 + (u64)_hex_val(c);
            p.pos++; i++;
        };
        if (cp <= 0x7F)
        {
            buf[*pos] = (byte)cp; *pos = *pos + 1;
        }
        elif (cp <= 0x7FF)
        {
            buf[*pos]     = (byte)(0xC0 | (cp >> 6));
            buf[*pos + 1] = (byte)(0x80 | (cp & 0x3F));
            *pos = *pos + 2;
        }
        elif (cp <= 0xFFFF)
        {
            buf[*pos]     = (byte)(0xE0 | (cp >> 12));
            buf[*pos + 1] = (byte)(0x80 | ((cp >> 6) & 0x3F));
            buf[*pos + 2] = (byte)(0x80 | (cp & 0x3F));
            *pos = *pos + 3;
        }
        else
        {
            buf[*pos]     = (byte)(0xF0 | (cp >> 18));
            buf[*pos + 1] = (byte)(0x80 | ((cp >> 12) & 0x3F));
            buf[*pos + 2] = (byte)(0x80 | ((cp >> 6) & 0x3F));
            buf[*pos + 3] = (byte)(0x80 | (cp & 0x3F));
            *pos = *pos + 4;
        };
        return true;
    };

    // ====================================================================
    // String parsers
    // ====================================================================

    def _parse_basic_string(_Parser* p) -> byte*
    {
        byte* buf;
        int   cap, out;
        byte  c;
        cap = p.len - p.pos + 1;
        buf = (byte*)fmalloc((size_t)cap);
        if ((u64)buf == 0) { return (byte*)0; };
        out = 0;
        while (p.pos < p.len)
        {
            c = p.src[p.pos];
            if (c == '"') { p.pos++; buf[out] = 0; return buf; };
            if (c == '\n' | c == '\r')
            {
                _set_error(p, "Newline in basic string\0");
                ffree((u64)buf); return (byte*)0;
            };
            if (c == '\\')
            {
                p.pos++;
                if (p.pos >= p.len) { _set_error(p, "Truncated escape\0"); ffree((u64)buf); return (byte*)0; };
                c = p.src[p.pos]; p.pos++;
                if      (c == '"') { buf[out] = '"'; out++; }
                elif    (c == '\\') { buf[out] = '\\'; out++; }
                elif    (c == 'b') { buf[out] = '\b'; out++; }
                elif    (c == 't') { buf[out] = '\t'; out++; }
                elif    (c == 'n') { buf[out] = '\n'; out++; }
                elif    (c == 'f') { buf[out] = '\f'; out++; }
                elif    (c == 'r') { buf[out] = '\r'; out++; }
                elif    (c == 'u')
                {
                    if (!_parse_unicode_escape(p, buf, @out, false))
                    { _set_error(p, "Bad \\u escape\0"); ffree((u64)buf); return (byte*)0; };
                }
                elif    (c == 'U')
                {
                    if (!_parse_unicode_escape(p, buf, @out, true))
                    { _set_error(p, "Bad \\U escape\0"); ffree((u64)buf); return (byte*)0; };
                }
                elif (c == '\n' | c == '\r' | c == ' ' | c == '\t')
                {
                    if (c == '\n') { p.line++; };
                    while (p.pos < p.len & (_is_ws(p.src[p.pos]) | _is_newline(p.src[p.pos])))
                    {
                        if (p.src[p.pos] == '\n') { p.line++; };
                        p.pos++;
                    };
                }
                else { _set_error(p, "Unknown escape\0"); ffree((u64)buf); return (byte*)0; };
            }
            else { buf[out] = c; out++; p.pos++; };
        };
        _set_error(p, "Unterminated basic string\0");
        ffree((u64)buf); return (byte*)0;
    };

    def _parse_ml_basic_string(_Parser* p) -> byte*
    {
        byte* buf;
        int   cap, out;
        byte  c;
        if (p.pos < p.len & p.src[p.pos] == '\n') { p.line++; p.pos++; }
        elif (p.pos + 1 < p.len & p.src[p.pos] == '\r' & p.src[p.pos + 1] == '\n')
        { p.line++; p.pos = p.pos + 2; };
        cap = p.len - p.pos + 1;
        buf = (byte*)fmalloc((size_t)cap);
        if ((u64)buf == 0) { return (byte*)0; };
        out = 0;
        while (p.pos < p.len)
        {
            c = p.src[p.pos];
            if (c == '"' & p.pos + 2 < p.len &
                p.src[p.pos + 1] == '"' & p.src[p.pos + 2] == '"')
            {
                p.pos = p.pos + 3;
                buf[out] = 0; return buf;
            };
            if (c == '\\')
            {
                p.pos++;
                if (p.pos >= p.len) { _set_error(p, "Truncated escape\0"); ffree((u64)buf); return (byte*)0; };
                c = p.src[p.pos]; p.pos++;
                if      (c == '"') { buf[out] = '"'; out++; }
                elif    (c == '\\') { buf[out] = '\\'; out++; }
                elif    (c == 'b') { buf[out] = '\b'; out++; }
                elif    (c == 't') { buf[out] = '\t'; out++; }
                elif    (c == 'n') { buf[out] = '\n'; out++; }
                elif    (c == 'f') { buf[out] = '\f'; out++; }
                elif    (c == 'r') { buf[out] = '\r'; out++; }
                elif    (c == 'u') { if (!_parse_unicode_escape(p, buf, @out, false)) { _set_error(p, "Bad \\u\0"); ffree((u64)buf); return (byte*)0; }; }
                elif    (c == 'U') { if (!_parse_unicode_escape(p, buf, @out, true))  { _set_error(p, "Bad \\U\0"); ffree((u64)buf); return (byte*)0; }; }
                elif (c == '\n' | c == '\r' | c == ' ' | c == '\t')
                {
                    if (c == '\n') { p.line++; };
                    while (p.pos < p.len & (_is_ws(p.src[p.pos]) | _is_newline(p.src[p.pos])))
                    { if (p.src[p.pos] == '\n') { p.line++; }; p.pos++; };
                }
                else { _set_error(p, "Unknown escape\0"); ffree((u64)buf); return (byte*)0; };
            }
            elif (c == '\n') { p.line++; buf[out] = c; out++; p.pos++; }
            elif (c == '\r')
            {
                p.pos++;
                if (p.pos < p.len & p.src[p.pos] == '\n') { p.line++; p.pos++; };
                buf[out] = '\n'; out++;
            }
            else { buf[out] = c; out++; p.pos++; };
        };
        _set_error(p, "Unterminated ml basic string\0");
        ffree((u64)buf); return (byte*)0;
    };

    def _parse_literal_string(_Parser* p) -> byte*
    {
        int   start, n;
        start = p.pos;
        while (p.pos < p.len & p.src[p.pos] != '\'' & p.src[p.pos] != '\n') { p.pos++; };
        if (p.pos >= p.len | p.src[p.pos] != '\'') { _set_error(p, "Unterminated literal string\0"); return (byte*)0; };
        n = p.pos - start;
        p.pos++;
        return _str_copy(@p.src[start], n);
    };

    def _parse_ml_literal_string(_Parser* p) -> byte*
    {
        byte* buf;
        int   cap, out;
        byte  c;
        if (p.pos < p.len & p.src[p.pos] == '\n') { p.line++; p.pos++; }
        elif (p.pos + 1 < p.len & p.src[p.pos] == '\r' & p.src[p.pos + 1] == '\n')
        { p.line++; p.pos = p.pos + 2; };
        cap = p.len - p.pos + 1;
        buf = (byte*)fmalloc((size_t)cap);
        if ((u64)buf == 0) { return (byte*)0; };
        out = 0;
        while (p.pos < p.len)
        {
            c = p.src[p.pos];
            if (c == '\'' & p.pos + 2 < p.len &
                p.src[p.pos + 1] == '\'' & p.src[p.pos + 2] == '\'')
            { p.pos = p.pos + 3; buf[out] = 0; return buf; };
            if (c == '\n') { p.line++; buf[out] = c; out++; p.pos++; }
            elif (c == '\r')
            {
                p.pos++;
                if (p.pos < p.len & p.src[p.pos] == '\n') { p.line++; p.pos++; };
                buf[out] = '\n'; out++;
            }
            else { buf[out] = c; out++; p.pos++; };
        };
        _set_error(p, "Unterminated ml literal string\0");
        ffree((u64)buf); return (byte*)0;
    };

    // ====================================================================
    // Number parsers
    // ====================================================================

    def _parse_integer(_Parser* p, i64* out) -> bool
    {
        i64  val;
        int  base;
        bool neg;
        byte c;
        neg = false; base = 10; val = 0;
        if (p.pos < p.len & p.src[p.pos] == '-') { neg = true;  p.pos++; }
        elif (p.pos < p.len & p.src[p.pos] == '+') { p.pos++; };
        if (p.pos + 1 < p.len & p.src[p.pos] == '0')
        {
            c = p.src[p.pos + 1];
            if (c == 'x') { base = 16; p.pos = p.pos + 2; }
            elif (c == 'o') { base = 8;  p.pos = p.pos + 2; }
            elif (c == 'b') { base = 2;  p.pos = p.pos + 2; };
        };
        if (p.pos >= p.len) { return false; };
        while (p.pos < p.len)
        {
            c = p.src[p.pos];
            if (c == '_') { p.pos++; continue; };
            if (base == 16 & _is_hex(c))   { val = val * 16 + (i64)_hex_val(c); p.pos++; }
            elif (base == 8  & _is_oct(c))  { val = val * 8  + (i64)(c - '0'); p.pos++; }
            elif (base == 2  & _is_bin(c))  { val = val * 2  + (i64)(c - '0'); p.pos++; }
            elif (base == 10 & _is_digit(c)) { val = val * 10 + (i64)(c - '0'); p.pos++; }
            else { break; };
        };
        if (neg) { val = -val; };
        *out = val;
        return true;
    };

    def _parse_float(_Parser* p, double* out) -> bool
    {
        byte[64] buf;
        int      i, j, exp_sign, exp_int, k;
        bool     neg;
        byte     c;
        double   result, frac, place, exp_val;
        i = 0; neg = false;
        if (p.pos + 3 <= p.len)
        {
            if (p.src[p.pos]=='i' & p.src[p.pos+1]=='n' & p.src[p.pos+2]=='f')
            { *out = 1.0 / 0.0; p.pos = p.pos + 3; return true; };
            if (p.src[p.pos]=='n' & p.src[p.pos+1]=='a' & p.src[p.pos+2]=='n')
            { *out = 0.0 / 0.0; p.pos = p.pos + 3; return true; };
        };
        if (p.pos + 4 <= p.len)
        {
            if (p.src[p.pos]=='+' & p.src[p.pos+1]=='i' & p.src[p.pos+2]=='n' & p.src[p.pos+3]=='f')
            { *out = 1.0 / 0.0; p.pos = p.pos + 4; return true; };
            if (p.src[p.pos]=='-' & p.src[p.pos+1]=='i' & p.src[p.pos+2]=='n' & p.src[p.pos+3]=='f')
            { *out = -(1.0 / 0.0); p.pos = p.pos + 4; return true; };
            if (p.src[p.pos]=='+' & p.src[p.pos+1]=='n' & p.src[p.pos+2]=='a' & p.src[p.pos+3]=='n')
            { *out = 0.0 / 0.0; p.pos = p.pos + 4; return true; };
            if (p.src[p.pos]=='-' & p.src[p.pos+1]=='n' & p.src[p.pos+2]=='a' & p.src[p.pos+3]=='n')
            { *out = 0.0 / 0.0; p.pos = p.pos + 4; return true; };
        };
        while (p.pos < p.len & i < 62)
        {
            c = p.src[p.pos];
            if (c == '_') { p.pos++; continue; };
            if (_is_digit(c) | c == '.' | c == 'e' | c == 'E' | c == '+' | c == '-')
            { buf[i] = c; i++; p.pos++; }
            else { break; };
        };
        buf[i] = 0;
        j = 0; result = 0.0; frac = 0.0; place = 0.1; exp_val = 1.0;
        exp_sign = 1; exp_int = 0;
        if (buf[j] == '-') { neg = true; j++; } elif (buf[j] == '+') { j++; };
        while (j < i & _is_digit(buf[j])) { result = result * 10.0 + (double)(buf[j] - '0'); j++; };
        if (j < i & buf[j] == '.')
        {
            j++;
            while (j < i & _is_digit(buf[j]))
            { frac = frac + (double)(buf[j] - '0') * place; place = place * 0.1; j++; };
            result = result + frac;
        };
        if (j < i & (buf[j] == 'e' | buf[j] == 'E'))
        {
            j++;
            if (j < i & buf[j] == '-') { exp_sign = -1; j++; } elif (j < i & buf[j] == '+') { j++; };
            while (j < i & _is_digit(buf[j])) { exp_int = exp_int * 10 + (int)(buf[j] - '0'); j++; };
            k = 0;
            while (k < exp_int)
            { if (exp_sign > 0) { exp_val = exp_val * 10.0; } else { exp_val = exp_val * 0.1; }; k++; };
            result = result * exp_val;
        };
        if (neg) { result = -result; };
        *out = result;
        return i > 0;
    };

    // ====================================================================
    // Key parser
    // ====================================================================

    def _parse_key_part(_Parser* p) -> byte*
    {
        byte c;
        int  start, n;
        c = _peek(p);
        if (c == '"')
        {
            p.pos++;
            if (p.pos + 1 < p.len & p.src[p.pos] == '"' & p.src[p.pos + 1] == '"')
            { p.pos = p.pos + 2; return _parse_ml_basic_string(p); };
            return _parse_basic_string(p);
        };
        if (c == '\'')
        {
            p.pos++;
            if (p.pos + 1 < p.len & p.src[p.pos] == '\'' & p.src[p.pos + 1] == '\'')
            { p.pos = p.pos + 2; return _parse_ml_literal_string(p); };
            return _parse_literal_string(p);
        };
        if (_is_bare_key(c))
        {
            start = p.pos;
            while (p.pos < p.len & _is_bare_key(p.src[p.pos])) { p.pos++; };
            n = p.pos - start;
            return _str_copy(@p.src[start], n);
        };
        _set_error(p, "Invalid key character\0");
        return (byte*)0;
    };

    def _navigate_key(_Parser* p, TomlTable* base, byte** last_key) -> TomlTable*
    {
        TomlTable* cur;
        byte*      part;
        TomlVal*   existing;
        cur = base;
        _skip_ws(p);
        part = _parse_key_part(p);
        if ((u64)part == 0) { return (TomlTable*)0; };
        _skip_ws(p);
        while (_expect(p, '.'))
        {
            p.pos++;
            _skip_ws(p);
            existing = (TomlVal*)_tbl_get(cur, part);
            if ((u64)existing == 0)
            {
                TomlTable* sub;
                TomlVal*   sv;
                sub = _tbl_new();
                if ((u64)sub == 0) { ffree((u64)part); return (TomlTable*)0; };
                sv = _val_table(sub);
                if ((u64)sv == 0) { ffree((u64)part); _tbl_free_contents(sub); ffree((u64)sub); return (TomlTable*)0; };
                if (!_tbl_set(cur, part, sv)) { ffree((u64)part); return (TomlTable*)0; };
                cur = sub;
            }
            elif (existing.type == TOML_TABLE) { ffree((u64)part); cur = existing.tbl; }
            elif (existing.type == TOML_ARRAY)
            {
                ffree((u64)part);
                if (existing.arr.count == 0) { _set_error(p, "Empty array of tables\0"); return (TomlTable*)0; };
                TomlVal* last_item;
                last_item = (TomlVal*)existing.arr.items[existing.arr.count - 1];
                if (last_item.type != TOML_TABLE) { _set_error(p, "Not a table in array\0"); return (TomlTable*)0; };
                cur = last_item.tbl;
            }
            else { ffree((u64)part); _set_error(p, "Key already exists as non-table\0"); return (TomlTable*)0; };
            part = _parse_key_part(p);
            if ((u64)part == 0) { return (TomlTable*)0; };
            _skip_ws(p);
        };
        *last_key = part;
        return cur;
    };

    // ====================================================================
    // Value parser (forward decl)
    // ====================================================================

    def _parse_value(_Parser* p)        -> TomlVal*;
    def _parse_array(_Parser* p)        -> TomlVal*;
    def _parse_inline_table(_Parser* p) -> TomlVal*;

    def _parse_value(_Parser* p) -> TomlVal*
    {
        byte   c, sc;
        byte*  s;
        i64    iv;
        double fv;
        int    look, scan;
        bool   is_float;
        _skip_ws(p);
        c = _peek(p);
        if (c == '"')
        {
            p.pos++;
            if (p.pos + 1 < p.len & p.src[p.pos] == '"' & p.src[p.pos + 1] == '"')
            { p.pos = p.pos + 2; s = _parse_ml_basic_string(p); }
            else { s = _parse_basic_string(p); };
            if ((u64)s == 0) { return (TomlVal*)0; };
            return _val_str(s);
        };
        if (c == '\'')
        {
            p.pos++;
            if (p.pos + 1 < p.len & p.src[p.pos] == '\'' & p.src[p.pos + 1] == '\'')
            { p.pos = p.pos + 2; s = _parse_ml_literal_string(p); }
            else { s = _parse_literal_string(p); };
            if ((u64)s == 0) { return (TomlVal*)0; };
            return _val_str(s);
        };
        if (c == '[') { return _parse_array(p); };
        if (c == '{') { return _parse_inline_table(p); };
        if (p.pos + 4 <= p.len &
            p.src[p.pos]=='t' & p.src[p.pos+1]=='r' &
            p.src[p.pos+2]=='u' & p.src[p.pos+3]=='e')
        { p.pos = p.pos + 4; return _val_bool(true); };
        if (p.pos + 5 <= p.len &
            p.src[p.pos]=='f' & p.src[p.pos+1]=='a' &
            p.src[p.pos+2]=='l' & p.src[p.pos+3]=='s' & p.src[p.pos+4]=='e')
        { p.pos = p.pos + 5; return _val_bool(false); };
        if (_is_digit(c) & p.pos + 4 < p.len &
            _is_digit(p.src[p.pos+1]) & _is_digit(p.src[p.pos+2]) &
            _is_digit(p.src[p.pos+3]) & p.src[p.pos+4] == '-')
        {
            int   dt_start, dt_len;
            byte* dt_s;
            dt_start = p.pos;
            while (p.pos < p.len)
            {
                byte dc;
                dc = p.src[p.pos];
                if (dc == ',' | dc == ']' | dc == '\n' | dc == '\r' | dc == '#') { break; };
                if (_is_ws(dc) & p.pos > dt_start)
                {
                    if (p.pos + 1 < p.len & _is_digit(p.src[p.pos + 1])) { p.pos++; }
                    else { break; };
                }
                else { p.pos++; };
            };
            dt_len = p.pos - dt_start;
            dt_s   = _str_copy(@p.src[dt_start], dt_len);
            if ((u64)dt_s == 0) { return (TomlVal*)0; };
            return _val_datetime(dt_s);
        };
        look = p.pos; is_float = false;
        if (look < p.len & (p.src[look] == '-' | p.src[look] == '+')) { look++; };
        if (look + 3 <= p.len &
            ((p.src[look]=='i' & p.src[look+1]=='n' & p.src[look+2]=='f') |
             (p.src[look]=='n' & p.src[look+1]=='a' & p.src[look+2]=='n')))
        { is_float = true; };
        if (!is_float)
        {
            scan = look;
            while (scan < p.len)
            {
                sc = p.src[scan];
                if (sc == '.' | sc == 'e' | sc == 'E') { is_float = true; break; };
                if (!_is_digit(sc) & sc != '_' & sc != '+' & sc != '-' &
                    sc != 'x' & sc != 'o' & sc != 'b' & !_is_hex(sc)) { break; };
                scan++;
            };
        };
        if (is_float)
        {
            if (!_parse_float(p, @fv)) { _set_error(p, "Invalid float\0"); return (TomlVal*)0; };
            return _val_float(fv);
        }
        else
        {
            if (!_parse_integer(p, @iv)) { _set_error(p, "Invalid value\0"); return (TomlVal*)0; };
            return _val_int(iv);
        };
    };

    def _parse_array(_Parser* p) -> TomlVal*
    {
        TomlArray* a;
        TomlVal*   v;
        p.pos++;
        a = _arr_new();
        if ((u64)a == 0) { return (TomlVal*)0; };
        _skip_ws_newline(p);
        while (p.pos < p.len & p.src[p.pos] != ']')
        {
            v = _parse_value(p);
            if ((u64)v == 0) { _arr_free(a); return (TomlVal*)0; };
            if (!_arr_push(a, v)) { _val_free(v); _arr_free(a); return (TomlVal*)0; };
            _skip_ws_newline(p);
            if (!_match(p, ',')) { break; };
            _skip_ws_newline(p);
        };
        if (!_match(p, ']')) { _set_error(p, "Unterminated array\0"); _arr_free(a); return (TomlVal*)0; };
        return _val_array(a);
    };

    def _parse_inline_table(_Parser* p) -> TomlVal*
    {
        TomlTable* t;
        byte*      key;
        TomlVal*   val;
        TomlTable* target;
        p.pos++;
        t = _tbl_new();
        if ((u64)t == 0) { return (TomlVal*)0; };
        _skip_ws(p);
        while (p.pos < p.len & p.src[p.pos] != '}')
        {
            target = _navigate_key(p, t, @key);
            if ((u64)target == 0 | (u64)key == 0)
            { if ((u64)key != 0) { ffree((u64)key); }; _tbl_free_contents(t); ffree((u64)t); return (TomlVal*)0; };
            _skip_ws(p);
            if (!_match(p, '='))
            { _set_error(p, "Expected = in inline table\0"); ffree((u64)key); _tbl_free_contents(t); ffree((u64)t); return (TomlVal*)0; };
            _skip_ws(p);
            val = _parse_value(p);
            if ((u64)val == 0) { ffree((u64)key); _tbl_free_contents(t); ffree((u64)t); return (TomlVal*)0; };
            if (!_tbl_set(target, key, val)) { ffree((u64)key); _val_free(val); _tbl_free_contents(t); ffree((u64)t); return (TomlVal*)0; };
            _skip_ws(p);
            if (!_match(p, ',')) { break; };
            _skip_ws(p);
        };
        if (!_match(p, '}')) { _set_error(p, "Unterminated inline table\0"); _tbl_free_contents(t); ffree((u64)t); return (TomlVal*)0; };
        return _val_table(t);
    };

    // ====================================================================
    // Table header parser
    // ====================================================================

    def _parse_table_header(_Parser* p) -> bool
    {
        bool       is_aot;
        TomlTable* target;
        byte*      key;
        TomlVal*   existing;
        is_aot = false;
        p.pos++;  // consume '['
        if (_match(p, '[')) { is_aot = true; };
        _skip_ws(p);
        target = _navigate_key(p, p.root, @key);
        if ((u64)target == 0 | (u64)key == 0)
        { if ((u64)key != 0) { ffree((u64)key); }; return false; };
        _skip_ws(p);
        if (!_match(p, ']')) { _set_error(p, "Expected ] after table key\0"); ffree((u64)key); return false; };
        if (is_aot & !_match(p, ']')) { _set_error(p, "Expected ]] after aot key\0"); ffree((u64)key); return false; };
        if (is_aot)
        {
            existing = (TomlVal*)_tbl_get(target, key);
            if ((u64)existing == 0)
            {
                TomlArray* arr;
                TomlVal*   av;
                arr = _arr_new();
                if ((u64)arr == 0) { ffree((u64)key); return false; };
                av = _val_array(arr);
                if ((u64)av == 0) { ffree((u64)key); _arr_free(arr); return false; };
                if (!_tbl_set(target, key, av)) { ffree((u64)key); _val_free(av); return false; };
                existing = av;
            }
            elif (existing.type != TOML_ARRAY)
            { _set_error(p, "Key already defined as non-array\0"); ffree((u64)key); return false; }
            else { ffree((u64)key); };
            TomlTable* new_tbl;
            TomlVal*   new_tv;
            new_tbl = _tbl_new();
            if ((u64)new_tbl == 0) { return false; };
            new_tv = _val_table(new_tbl);
            if ((u64)new_tv == 0) { _tbl_free_contents(new_tbl); ffree((u64)new_tbl); return false; };
            if (!_arr_push(existing.arr, new_tv)) { _val_free(new_tv); return false; };
            p.cur = new_tbl;
        }
        else
        {
            existing = (TomlVal*)_tbl_get(target, key);
            if ((u64)existing == 0)
            {
                TomlTable* new_tbl;
                TomlVal*   new_tv;
                new_tbl = _tbl_new();
                if ((u64)new_tbl == 0) { ffree((u64)key); return false; };
                new_tv = _val_table(new_tbl);
                if ((u64)new_tv == 0) { ffree((u64)key); _tbl_free_contents(new_tbl); ffree((u64)new_tbl); return false; };
                if (!_tbl_set(target, key, new_tv)) { ffree((u64)key); _val_free(new_tv); return false; };
                p.cur = new_tbl;
            }
            elif (existing.type == TOML_TABLE) { ffree((u64)key); p.cur = existing.tbl; }
            else { _set_error(p, "Key already defined\0"); ffree((u64)key); return false; };
        };
        return true;
    };

    // ====================================================================
    // Key-value line
    // ====================================================================

    def _parse_keyval(_Parser* p) -> bool
    {
        byte*      key;
        TomlVal*   val;
        TomlTable* target;
        target = _navigate_key(p, p.cur, @key);
        if ((u64)target == 0 | (u64)key == 0)
        { if ((u64)key != 0) { ffree((u64)key); }; return false; };
        _skip_ws(p);
        if (!_match(p, '=')) { _set_error(p, "Expected =\0"); ffree((u64)key); return false; };
        _skip_ws(p);
        val = _parse_value(p);
        if ((u64)val == 0) { ffree((u64)key); return false; };
        if (!_tbl_set(target, key, val)) { ffree((u64)key); _val_free(val); return false; };
        return true;
    };

    // ====================================================================
    // Main parse loop
    // ====================================================================

    def toml_parse(byte* src, int len, TomlTable* out) -> bool
    {
        _Parser p;
        out.keys     = (byte**)fmalloc((size_t)64);
        out.vals     = (void**)fmalloc((size_t)64);
        out.count    = 0;
        out.capacity = 8;
        out.error    = (byte*)0;
        if ((u64)out.keys == 0 | (u64)out.vals == 0) { return false; };
        p.src  = src;
        p.len  = len;
        p.pos  = 0;
        p.line = 1;
        p.root = out;
        p.cur  = out;
        while (p.pos < p.len)
        {
            byte c;
            _skip_ws_newline(@p);
            if (p.pos >= p.len) { break; };
            c = p.src[p.pos];
            if (c == '[')
            {
                if (!_parse_table_header(@p)) { return false; };
                _skip_ws(@p);
                if (p.pos < p.len & p.src[p.pos] == '#') { _skip_line(@p); }
                elif (p.pos < p.len & !_is_newline(p.src[p.pos]) & p.src[p.pos] != 0)
                { _set_error(@p, "Garbage after table header\0"); return false; };
            }
            elif (_is_bare_key(c) | c == '"' | c == '\'')
            {
                if (!_parse_keyval(@p)) { return false; };
                _skip_ws(@p);
                if (p.pos < p.len & p.src[p.pos] == '#') { _skip_line(@p); }
                elif (p.pos < p.len & !_is_newline(p.src[p.pos]) & p.src[p.pos] != 0)
                { _set_error(@p, "Garbage after key-value pair\0"); return false; };
            };
        };
        return (u64)out.error == 0;
    };

    // ====================================================================
    // Public API
    // ====================================================================

    def toml_error(TomlTable* t) -> byte* { return t.error; };

    def toml_get(TomlTable* t, byte* key) -> TomlVal*
    { return (TomlVal*)_tbl_get(t, key); };

    def toml_get_str(TomlTable* t, byte* key) -> byte*
    {
        TomlVal* v;
        v = toml_get(t, key);
        if ((u64)v == 0 | v.type != TOML_STRING) { return (byte*)0; };
        return v.s;
    };

    def toml_get_int(TomlTable* t, byte* key) -> i64
    {
        TomlVal* v;
        v = toml_get(t, key);
        if ((u64)v == 0 | v.type != TOML_INT) { return 0; };
        return v.i;
    };

    def toml_get_float(TomlTable* t, byte* key) -> double
    {
        TomlVal* v;
        v = toml_get(t, key);
        if ((u64)v == 0 | v.type != TOML_FLOAT) { return 0.0; };
        return v.f;
    };

    def toml_get_bool(TomlTable* t, byte* key) -> bool
    {
        TomlVal* v;
        v = toml_get(t, key);
        if ((u64)v == 0 | v.type != TOML_BOOL) { return false; };
        return (bool)v.i;
    };

    def toml_get_table(TomlTable* t, byte* key) -> TomlTable*
    {
        TomlVal* v;
        v = toml_get(t, key);
        if ((u64)v == 0 | v.type != TOML_TABLE) { return (TomlTable*)0; };
        return v.tbl;
    };

    def toml_get_array(TomlTable* t, byte* key) -> TomlArray*
    {
        TomlVal* v;
        v = toml_get(t, key);
        if ((u64)v == 0 | v.type != TOML_ARRAY) { return (TomlArray*)0; };
        return v.arr;
    };

}; // namespace toml

#endif; // FLUX_TOML
